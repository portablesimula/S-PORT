begin Boolean Listing;      ! Output Listing Indicator ;
      Ref(InByteFile) Inpt; ! NRF-Input File ;
      Ref(OutFile) Oupt;    ! Created Output File ;

      Integer Sequ;         ! Input byte number ;
      Integer Start;        ! Start byte number ;
      Integer Count;        ! Count byte number ;
      Integer Code;         ! Current Record Code ;

      Text Procedure Prompt(ms,meny); value ms,meny; text ms,meny;
      begin text R; L: OutText(Cat(ms,": ")); BreakOutImage; inimage;
            R:-SysIn.image.strip; R.SetPos(R.length);
            if if R==notext then true else R.GetChar='?' then
            begin OutText("Input Format: "); OutText(meny);
                  OutImage; goto L;
            end;  Prompt:-copy(R); SysIn.SetPos(SysIn.length+1);
            OutText(R); OutImage;
      end *** Prompt ***;

      Procedure InputFile;
      begin text F; S: F:-Prompt("Input File","Standard NORD File Format");
            F:-Cat(F,":OBJ"); Inpt:-new InByteFile(F);
            if not Inpt.Open then
            begin OutImage; OutText("The File """); OutText(F);
                  OutText(""" does not exist or is not accessible");
                  OutImage; OutImage; goto S;
            end;
      end;

      Procedure OutputFile;
      begin text F; S: F:-Prompt("Output File","Standard NORD File Format");
            if F="TERM" then Oupt:-SysOut else
            begin F:-Cat(F,":SYMB"); Oupt:-new OutFile(F);
                  if not Oupt.Open(Blanks(100)) then
                  begin OutImage; OutText("The File """); OutText(F);
                        OutText(""" does not exist or is not accessible");
                        OutImage; OutImage; goto S;
                  end;
            end;
      end;

      Text Procedure Cat(t1,t2); value t1,t2; text t1,t2;
      begin text t; Cat:-t:-Blanks(t1.length+t2.length);
            t:=t1; t.Sub(t1.length+1,t2.length):=t2;
      end *** Cat ***;

      Integer Procedure InByte;
      begin InByte:=Inpt.InByte; Sequ:=Sequ+1 end;

      Procedure OutByte(n); integer n; inspect Oupt do
      begin integer a,b,c; a:=n//64;
            b:=mod(n,64)//8; c:=mod(n,8);
            OutChar(' '); OutInt(a,1); OutInt(b,1); OutInt(c,1);
      end;

%title ***    M O N I T O R    ***

      OutText("FILE DUMP");
      OutImage; OutImage; InputFile; OutputFile;
      Start:=Prompt("Start Byte","0,1, ...").GetInt;
      Count:=Prompt("Item Count","1,2, ...").GetInt;

NEXT:
      if Listing then Oupt.OutImage else
      begin Oupt.SetPos(1); Oupt.image:=notext end;

      if Inpt.EndFile then goto FERDIG;
      Code:=InByte; if Inpt.EndFile then goto FERDIG;
      if Listing then
      begin Count:=Count-1;
            if Count < 1 then
            begin Count:=Prompt("Item Count","0,1,2, ...").GetInt;
                  if Count <= 0 then goto FERDIG;
            end;
      end else Listing := Sequ >= Start;
      Oupt.OutInt(Sequ,4); Oupt.OutText(": ");

      OutByte(code);
      while Oupt.Pos < 45 do
      begin Code:=InByte; if Inpt.EndFile then goto NEXT;
            OutByte(code);
      end;
      goto NEXT;

FERDIG:

      if Oupt =/= SysOut then Oupt.Close;
      Inpt.Close;
      OutImage; OutImage;
      OutText("END  --  Input Bytes"); OutInt(Sequ-1,6);
      OutImage; OutImage;
end;




